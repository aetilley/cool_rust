// build.rs uses this language specification to generate the parser ('cool_grammar.rs').

use crate::token::{Token, LexicalError, StringLiteralError};
use crate::ast::{add_one, Program, Classes, Class, Features, Feature, Expr, Exprs, Formals, Formal, Case, Cases, LetBinding, LetBindings};

grammar;

extern {
  type Location = usize;
  type Error = LexicalError;
  enum Token{
    ";" => Token::Semicolon,
    ":" => Token::Colon,
    "class" => Token::Class,
    "typeid" => Token::TypeId(<String>),
    "objectid" => Token::ObjectId(<String>),
    "bool_const" => Token::BoolConst(<bool>),
    "int_const" => Token::IntConst(<String>),
    "str_const" => Token::StrConst(<Result<String, StringLiteralError>>),
    "{" => Token::LCurly,
    "}" => Token::RCurly,
    "(" => Token::LParen,
    ")" => Token::RParen,
    "<-" => Token::Assign,
    "," => Token::Comma,
    "new" => Token::New,
    "if" => Token::If,
    "then" => Token::Then,
    "else" => Token::Else,
    "fi" => Token::Fi,
    "of" => Token::Of,
    "while" => Token::While,
    "inherits" => Token::Inherits,
    "loop" => Token::Loop,
    "pool" => Token::Pool,
    "." => Token::Period,
    "@" => Token::At,
    "~" => Token::Comp,
    "isvoid" => Token::IsVoid,
    "=" => Token::Eq,
    "<" => Token::LT,
    "<=" => Token::LTE,
    "not" => Token::Not,
    "+" => Token::Plus,
    "-" => Token::Minus,
    "*" => Token::Times,
    "/" => Token::Divide,
    "let" => Token::Let,
    "in" => Token::In,
    "error" => Token::Error(_),
    "case" => Token::Case,
    "esac" => Token::Esac,
    "=>" => Token::DArrow,
  }
}

pub ProgramTy: Program = {
  <clist: ClassesTy> => Program::program(clist),
}

pub ClassTy: Class = {
  "class" <name: "typeid"> "{" <features: FeaturesTy> "}" ";"=> Class::class(name, "Object".to_string(), features),
  "class" <name: "typeid"> "inherits" <parent: "typeid"> "{" <features: FeaturesTy> "}" ";"=> Class::class(name, parent, features),
}

pub ClassesTy: Classes = {
  <c: ClassTy> => vec![c],
  <cs: ClassesTy> <c: ClassTy> => add_one(&cs, &c),
}

pub FormalTy: Formal = {
  <name: "objectid"> ":" <typ: "typeid">  => Formal::formal(name, typ),
}

pub _SomeFormalsTy: Formals = {
  <f: FormalTy> => vec![f],
  <some: _SomeFormalsTy> "," <f: FormalTy> => add_one(&some, &f), 
}

pub FormalsTy: Formals = {
  () => vec![],
  <some: _SomeFormalsTy> => some,
}

pub FeatureTy: Feature = {
  <name: "objectid"> ":" <typ: "typeid"> ";" => Feature::attr(name, typ, Expr::no_expr()),
  <name: "objectid"> ":" <typ: "typeid"> "<-" <expr: ExprTy> ";" => Feature::attr(name, typ, expr),
  <name: "objectid"> "(" <formals: FormalsTy> ")" ":" <typ: "typeid"> "{" <expr: ExprTy> "}" ";" => Feature::method(name, formals, typ, expr), 
}

pub FeaturesTy: Features = {
  () => vec![],
  <fs: FeaturesTy> <f: FeatureTy>  => add_one(&fs, &f), 
}

pub ExprTy: Expr = {
  // Precedence 10

  #[precedence(level="10")]
  "{" <exprs: ExprsWithSemicolonsTy> "}" => Expr::block(exprs),

  #[precedence(level="10")]
  "new" <typ: "typeid"> => Expr::new(typ),
  #[precedence(level="10")]
  "while" <cond: ExprTy> "loop" <body: ExprTy> "pool" => Expr::r#loop(cond, body),
  #[precedence(level="10")]
  "case" <expr: ExprTy> "of" <cases: CasesTy> "esac" => Expr::typcase(expr, cases),
  #[precedence(level="10")]
  "let" <let_bindings: LetBindingsTy> "in" <body: ExprTy> => LetBinding::unwrap_let_bindings(let_bindings, body),

  
  // Precedence 9
  #[precedence(level="9")] #[assoc(side="right")]
  <var: "objectid"> "<-" <expr: ExprTy> => Expr::assign(var, expr),

  // Precedence 8
  #[precedence(level="8")]
  "not" <e: ExprTy> => Expr::not(e),

  // Precedence 7
  #[precedence(level="7")] #[assoc(side="left")]
  <e1: ExprTy> "=" <e2: ExprTy> => Expr::eq(e1, e2),
  #[precedence(level="7")] #[assoc(side="left")]
  <e1: ExprTy> "<=" <e2: ExprTy> => Expr::leq(e1, e2),
  #[precedence(level="7")] #[assoc(side="left")]
  <e1: ExprTy> "<" <e2: ExprTy> => Expr::lt(e1, e2),

  // Precedence 6
  #[precedence(level="6")] #[assoc(side="left")]
  <e1: ExprTy> "+" <e2: ExprTy> => Expr::plus(e1, e2),
  #[precedence(level="6")] #[assoc(side="left")]
  <e1: ExprTy> "-" <e2: ExprTy> => Expr::minus(e1, e2),

  // Precedence 5
  #[precedence(level="5")] #[assoc(side="left")]
  <e1: ExprTy> "*" <e2: ExprTy> => Expr::times(e1, e2),
  #[precedence(level="5")] #[assoc(side="left")]
  <e1: ExprTy> "/" <e2: ExprTy> => Expr::divide(e1, e2),

  // Precedence 4
  #[precedence(level="4")]
  "isvoid" <expr: ExprTy> => Expr::isvoid(expr),

  // Precedence 3
  #[precedence(level="3")]
  "~" <expr: ExprTy> => Expr::comp(expr),

  // Precedence 2
  #[precedence(level="2")]
  <slf: ExprTy> "@" <typ: "typeid"> "." <method_name: "objectid"> "(" <exprs: CommaSepExprsTy> ")" => Expr::static_dispatch(slf, typ, method_name, exprs),

  // Precedence 1
  #[precedence(level="1")]
  <slf: ExprTy> "." <method_name: "objectid"> "(" <exprs: CommaSepExprsTy> ")" => Expr::dispatch(slf, method_name, exprs),
    #[precedence(level="1")]
  <method_name: "objectid"> "(" <exprs: CommaSepExprsTy> ")" => Expr::dispatch(Expr::no_expr(), method_name, exprs),
  #[precedence(level="1")]
  "if" <pred: ExprTy> "then" <then_expr: ExprTy> "else" <else_expr: ExprTy> "fi" => Expr::cond(pred, then_expr, else_expr),

  #[precedence(level="0")]
  <name: "objectid"> => Expr::object(name),
  #[precedence(level="0")]
  <value: "int_const"> => Expr::int_const(value),
  #[precedence(level="0")]
  <maybe_value: "str_const"> => Expr::str_const(maybe_value.unwrap()),
  #[precedence(level="0")]
  <value: BoolConstTy> => Expr::bool_const(value),
    #[precedence(level="0")] 
  "(" <expr: ExprTy> ")" => expr,
}

pub BoolConstTy: bool = {
  <value: "bool_const"> => value,
}

pub CommaSepExprsTy: Exprs = {
  () => vec![],
  <some: _SomeCommaSepExprTy> => some,
}

pub _SomeCommaSepExprTy: Exprs = {
  <e: ExprTy> => vec![e],
  <some: _SomeCommaSepExprTy> "," <f: ExprTy> => add_one(&some, &f),
}

pub ExprsWithSemicolonsTy: Exprs = {
  <e: ExprTy> ";" => vec![e],
  <es: ExprsWithSemicolonsTy> <e: ExprTy> ";" => add_one(&es, &e),

}

pub CaseTy : Case = {
  <name: "objectid"> ":" <typ: "typeid"> "=>" <expr: ExprTy> ";" => Case::case(name, typ, expr),
}

pub CasesTy: Cases = {
  <case: CaseTy> => vec![case],
  <cs: CasesTy> <c: CaseTy> => add_one(&cs, &c),
}

pub LetBindingTy: LetBinding = {
  <id: "objectid"> ":" <typ: "typeid"> => LetBinding::let_binding(id, typ, Expr::no_expr()),
  <id: "objectid"> ":" <typ: "typeid"> "<-" <expr: ExprTy> => LetBinding::let_binding(id, typ, expr),
}

pub LetBindingsTy : LetBindings = {
  <binding: LetBindingTy> => vec![binding],
  <bs: LetBindingsTy> "," <b: LetBindingTy> => add_one(&bs, &b),
}
